NFTs Bootcamp - 02
Solange Gueiros
NFT Brasil - Set/2024

https://pad.riseup.net/p/nftbrasil-03

Inscricoes no Hackathon
https://taikai.network/NFTBrasil/hackathons/hackathoNFBrasil/

Slides
https://docs.google.com/presentation/d/1SktvN7CX7IZVAgK7iT2M8Z5pJHngvIousgPewbU8G7A/pub?start=false&loop=false&delayms=3000

Aula 01
https://www.youtube.com/watch?v=KTOxNw65wBU

Aula 02
https://www.youtube.com/watch?v=FekcGFHMPK4

Aula 03
https://www.youtube.com/watch?v=XJ3FjuDLPAQ

Nome - Rede social (se quiser)
Sol - solangegueiros
MAIKAO - @maikao_official
Jeff Souza - instagram.com/j3ffsouza
Marina Wentzel - marinawentzel@gmail.com
Raphael - @raphaelrcarvalho
Alexandre Eduardo - @aleedu_avelino
Jos√© Aldo Teixeira
Pedr0x - https://warpcast.com/pedr0x.eth


**********************************

https://remix.ethereum.org/
Icone 5 - DEPLOY & RUN TRANSACTIONS
ENVIRONMENT
Injected provider - Metamask
Check the Sepolia (11155111) network

Icone 2 - FILE EXPLORER
Criar o arquivo
CrossChainPriceNFT.sol

********************************* In√≠cio do c√≥digo *********************************
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.19;

// Deploy this contract on Sepolia

// Importing OpenZeppelin contracts
import "@openzeppelin/contracts@4.6.0/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts@4.6.0/utils/Counters.sol";
import "@openzeppelin/contracts@4.6.0/utils/Base64.sol";

// Importing Chainlink contracts
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";

contract CrossChainPriceNFT is ERC721, ERC721URIStorage {
    using Counters for Counters.Counter;
    using Strings for uint256;

    Counters.Counter public tokenIdCounter;

    // Create price feed
    AggregatorV3Interface internal priceFeed;
    uint256 public lastPrice = 0;

    string priceIndicatorUp = unicode"üòÄ";
    string priceIndicatorDown = unicode"üòî";
    string priceIndicatorFlat = unicode"üòë";
    string public priceIndicator;

    struct ChainStruct {
        uint64 code;
        string name;
        string color;
    }
    mapping (uint256 => ChainStruct) chain;

    //https://docs.chain.link/ccip/supported-networks/testnet
    constructor() ERC721("CrossChain Price", "CCPrice") {
        chain[0] = ChainStruct ({
            code: 16015286601757825753,
            name: "Sepolia",
            color: "#0000ff" //blue
        });
        chain[1] = ChainStruct ({
            code: 14767482510784806043,
            name: "Fuji",
            color: "#ff0000" //red
        });
        chain[2] = ChainStruct ({
            code: 12532609583862916517,
            name: "Mumbai",
            color: "#4b006e" //purple
        });

        // https://docs.chain.link/data-feeds/price-feeds/addresses        
        priceFeed = AggregatorV3Interface(
            // Sepolia BTC/USD
            0x1b44F3514812d835EB1BDB0acB33d3fA3351Ee43            
        );

        // Mint an NFT
        mint(msg.sender);
    }

    function mint(address to) public {
        // Mint from Sepolia network = chain[0]
        mintFrom(to, 0);
    }

    function mintFrom(address to, uint256 sourceId) public {
        // sourceId 0 Sepolia, 1 Fuji, 2 Mumbai
        uint256 tokenId = tokenIdCounter.current();
        _safeMint(to, tokenId);
        updateMetaData(tokenId, sourceId);    
        tokenIdCounter.increment();
    }

    // Update MetaData
    function updateMetaData(uint256 tokenId, uint256 sourceId) public {
        // Create the SVG string
        string memory finalSVG = buildSVG(sourceId);
           
        // Base64 encode the SVG
        string memory json = Base64.encode(
            bytes(
                string(
                    abi.encodePacked(
                        '{"name": "Cross-chain Price SVG",',
                        '"description": "SVG NFTs in different chains",',
                        '"image": "data:image/svg+xml;base64,',
                        Base64.encode(bytes(finalSVG)), '",',
                        '"attributes": [',
                            '{"trait_type": "source",',
                            '"value": "', chain[sourceId].name ,'"},',
                            '{"trait_type": "price",',
                            '"value": "', lastPrice.toString() ,'"}',
                        ']}'
                    )
                )
            )
        );
        // Create token URI
        string memory finalTokenURI = string(
            abi.encodePacked("data:application/json;base64,", json)
        );
        // Set token URI
        _setTokenURI(tokenId, finalTokenURI);
    }

    // Build the SVG string
    function buildSVG(uint256 sourceId) internal returns (string memory) {

        // Create SVG rectangle with random color
        string memory headSVG = string(
            abi.encodePacked(
                "<svg xmlns='http://www.w3.org/2000/svg' version='1.1' xmlns:xlink='http://www.w3.org/1999/xlink' xmlns:svgjs='http://svgjs.com/svgjs' width='500' height='500' preserveAspectRatio='none' viewBox='0 0 500 500'> <rect width='100%' height='100%' fill='",
                chain[sourceId].color,
                "' />"
            )
        );
        // Update emoji based on price
        string memory bodySVG = string(
            abi.encodePacked(
                "<text x='50%' y='50%' font-size='128' dominant-baseline='middle' text-anchor='middle'>",
                comparePrice(),
                "</text>"
            )
        );
        // Close SVG
        string memory tailSVG = "</svg>";

        // Concatenate SVG strings
        string memory _finalSVG = string(
            abi.encodePacked(headSVG, bodySVG, tailSVG)
        );
        return _finalSVG;
    }

    // Compare new price to previous price
    function comparePrice() public returns (string memory) {
        uint256 currentPrice = getChainlinkDataFeedLatestAnswer();
        if (currentPrice > lastPrice) {
            priceIndicator = priceIndicatorUp;
        } else if (currentPrice < lastPrice) {
            priceIndicator = priceIndicatorDown;
        } else {
            priceIndicator = priceIndicatorFlat;
        }
        lastPrice = currentPrice;
        return priceIndicator;
    }

    function getChainlinkDataFeedLatestAnswer() public view returns (uint256) {
        (, int256 price, , , ) = priceFeed.latestRoundData();
        return uint256(price);
    }

    function tokenURI(uint256 tokenId)
        public view override(ERC721, ERC721URIStorage) returns (string memory)
    {
        return super.tokenURI(tokenId);
    }

    // The following function is an override required by Solidity.
    function _burn(uint256 tokenId) internal override(ERC721, ERC721URIStorage)
    {
        super._burn(tokenId);
    }
}
```
****************************** Fim do c√≥digo *************************************************

#### Explicando o contrato CrossChainPriceNFT.sol

1. Licen√ßa, Pragma e Coment√°rio de Deploy:
- // SPDX-License-Identifier: MIT: Esta linha especifica a licen√ßa MIT usada pelo contrato.
- pragma solidity 0.8.19;: Define a vers√£o Solidity utilizada para compilar o contrato (vers√£o 0.8.19).
- // Deploy this contract on Sepolia: Um coment√°rio lembrando que o contrato deve ser implantado na rede de teste Sepolia.

2. Importa√ß√µes de Bibliotecas
- Estas linhas importam bibliotecas necess√°rias para o funcionamento do contrato:
    - @openzeppelin/contracts/‚Ä¶: Importa funcionalidades para tokens ERC721 (ERC721URIStorage) e utilit√°rios como contador (Counters) e codifica√ß√£o base64 (Base64).
    - @chainlink/contracts/‚Ä¶: Importa interfaces para interagir com o servi√ßo de or√°culo de pre√ßos Chainlink (AggregatorV3Interface).

3. Defini√ß√£o do Contrato e Contador
- contract CrossChainPriceNFT is ERC721, ERC721URIStorage: Esta linha define o contrato CrossChainPriceNFT que herda funcionalidades de ambos os padr√µes ERC721 (NFT b√°sico) e ERC721URIStorage (permite armazenar a URI do token na blockchain).
- using Counters for Counters.Counter;: Habilita o uso de fun√ß√µes de contador para o tokenIdCounter.
- using Strings for uint256;: Permite converter valores inteiros em strings (necess√°rio para criar JSON).

4. Pre√ßo do Or√°culo e Emojis
- Counters.Counter public tokenIdCounter;: Declara um contador p√∫blico para atribuir IDs √∫nicos a cada NFT cunhado.
- AggregatorV3Interface internal priceFeed;: Esta linha declara uma vari√°vel interna do tipo AggregatorV3Interface para interagir com o contrato Chainlink que fornece o pre√ßo do ativo.
- uint256 public lastPrice = 0;: Armazena o √∫ltimo pre√ßo obtido do feed de dados.
- Tr√™s vari√°veis string definem emojis indicadores para varia√ß√£o de pre√ßo (priceIndicatorUp, priceIndicatorDown, e priceIndicatorFlat).

5. Estrutura ChainStruct e Construtor
- struct ChainStruct { ... }: Define uma estrutura nomeada ChainStruct para armazenar informa√ß√µes sobre as blockchains suportadas:
    - uint64 code: C√≥digo √∫nico da blockchain.
    - string name: Nome da blockchain.
    - string color: Cor que representa a blockchain em formato HTML.
- O construtor constructor() executa as seguintes a√ß√µes:
    - Inicializa o padr√£o ERC721 com um nome ("CrossChain Price") e s√≠mbolo ("CCPrice").
    - Cria entradas no mapeamento chain para tr√™s redes de teste (Sepolia, Fuji, Mumbai) com seus c√≥digos, nomes e cores.
    - Define a vari√°vel priceFeed para o endere√ßo do feed de pre√ßo BTC/USD da rede Sepolia.
    - Cria automaticamente um NFT inicial para o deployer do contrato (usando mint(msg.sender)).

6. Fun√ß√£o mint
- function mint(address to) public { ... }: Esta fun√ß√£o p√∫blica permite cunhar um novo NFT para um determinado endere√ßo (to).
- Internamente, ela chama mintFrom(to, 0) que assume a cria√ß√£o do NFT na rede Sepolia (ID da rede 0).

7. Fun√ß√£o mintFrom
- function mintFrom(address to, uint256 sourceId) public { ... }: Esta fun√ß√£o p√∫blica permite cunhar um NFT especificando a ID da rede de origem (sourceId).
- Recupera a ID atual do token do contador.
- Cria um novo NFT com essa ID para o destinat√°rio (to).
- Chama a fun√ß√£o updateMetaData para gerar e definir a URI inicial do token com base na rede de origem.
Incrementa o contador de ID do token.

8. Fun√ß√£o updateMetaData
- function updateMetaData(uint256 tokenId, uint256 sourceId) public { ... }: Esta fun√ß√£o p√∫blica atualiza os metadados de um NFT existente.
    - Chama a fun√ß√£o interna buildSVG para gerar uma string SVG representando o NFT.
    - Codifica a string SVG em base64 usando a biblioteca Base64.
    - Cria uma string JSON contendo os metadados do NFT, incluindo nome, descri√ß√£o, imagem (SVG codificado) e atributos (rede de origem e pre√ßo).
    - Combina a string JSON com um prefixo para criar o URI final do token.
    - Define o URI final do token usando _setTokenURI.

9. Fun√ß√£o buildSVG
- function buildSVG(uint256 sourceId) internal returns (string memory) { ... }: Esta fun√ß√£o interna cria a string SVG que representa o NFT.
    - Gera o c√≥digo SVG para um ret√¢ngulo com a cor correspondente √† rede de origem.
    - Gera o c√≥digo SVG para um texto que exibe o emoji indicando a varia√ß√£o de pre√ßo.
    - Combina os c√≥digos SVG do ret√¢ngulo e do texto para criar o SVG final.

10. Fun√ß√£o comparePrice
- function comparePrice() public returns (string memory) { ... }: Esta fun√ß√£o p√∫blica compara o pre√ßo atual com o pre√ßo anterior e retorna o emoji correspondente.
    - Obt√©m o pre√ßo atual usando getChainlinkDataFeedLatestAnswer.
    - Compara o pre√ßo atual com o pre√ßo anterior e atualiza a vari√°vel priceIndicator com o emoji adequado.
    - Retorna o emoji indicando a varia√ß√£o de pre√ßo.

11. Fun√ß√£o getChainlinkDataFeedLatestAnswer
- function getChainlinkDataFeedLatestAnswer() public view returns (uint256) { ... }: Esta fun√ß√£o p√∫blica obt√©m o √∫ltimo pre√ßo do feed de dados Chainlink.
    - Utiliza a fun√ß√£o latestRoundData do contrato AggregatorV3Interface para obter os dados da √∫ltima rodada.
    - Retorna o valor do pre√ßo como um uint256.

12. Fun√ß√µes tokenURI e _burn
- function tokenURI(uint256 tokenId) ...: Sobrescreve a fun√ß√£o tokenURI para retornar o URI do token.
- function _burn(uint256 tokenId) internal override ...: Sobrescreve a fun√ß√£o _burn para queimar um NFT.

----------------------------------------------------------------------------------------------------
Icone 5 - DEPLOY & RUN TRANSACTIONS
Deploy

Nome - CrossChainPriceNFT address
Sol - 0xa372c44DA07690E537e4fD0b8EA1b4b5ef50A956
MAIKAO - 0x097Ae1A53Dd12c056a1add95883E307ec23244d7
Jeff Souza - 0xeA6CB05A58214c45cCA9FB46666A89e681FdF922
Marina - 0xA90AaCB9F6C72139Cb29CbdD5e6C0bFDcdFc0ffF
Alexandre Eduardo - 0x59e595849D90c854638860ff93fC2099780ce491
Pedr0x - 0xD49CA935B6F7805bE5f3c8F04466526E0855E7C5
Raphael - 0x11429313030122cA0D00E54e926100845dF454b9



************************************
https://data.chain.link/
https://docs.chain.link/data-feeds/price-feeds/addresses

https://docs.chain.link/ccip/supported-networks/testnet

https://testnets.opensea.io/

Nome - Link da cole√ß√£o
Sol - https://testnets.opensea.io/collection/crosschain-price-1705
Jeff Souza - https://testnets.opensea.io/collection/crosschain-price-1702
Pedr0x - https://testnets.opensea.io/collection/crosschain-price-1707
Alexandre - https://testnets.opensea.io/collection/crosschain-price-1706
Raphael - https://testnets.opensea.io/collection/crosschain-price-1708
Jos√© Aldo - https://testnets.opensea.io/collection/crosschain-price-1709



*****************************

No Remix
Mint - para sua carteira

Price
56103.42413254
56177.00000000


Criar
CrossDestinationMinter.sol

********************************* In√≠cio do c√≥digo *********************************
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.19;

// Deploy this contract on Sepolia

import {CCIPReceiver} from "@chainlink/contracts-ccip/src/v0.8/ccip/applications/CCIPReceiver.sol";
import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";

interface InftMinter {
    function mintFrom(address account, uint256 sourceId) external;
}

/**
 * THIS IS AN EXAMPLE CONTRACT THAT USES HARDCODED VALUES FOR CLARITY.
 * THIS IS AN EXAMPLE CONTRACT THAT USES UN-AUDITED CODE.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract CrossDestinationMinter is CCIPReceiver {
    InftMinter public nft;

    event MintCallSuccessfull();
    // https://docs.chain.link/ccip/supported-networks/testnet
    address routerSepolia = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;

    constructor(address nftAddress) CCIPReceiver(routerSepolia) {
        nft = InftMinter(nftAddress);
    }

    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (bool success, ) = address(nft).call(message.data);
        require(success);
        emit MintCallSuccessfull();
    }

    function testMint() external {
        // Mint from Sepolia
        nft.mintFrom(msg.sender, 0);
    }

    function testMessage() external {
        // Mint from Sepolia
        bytes memory message;
        message = abi.encodeWithSignature("mintFrom(address,uint256)", msg.sender, 0);

        (bool success, ) = address(nft).call(message);
        require(success);
        emit MintCallSuccessfull();
    }

    function updateNFT(address nftAddress) external {
        nft = InftMinter(nftAddress);
    }
}
```
********************************* Fim do c√≥digo *********************************

#### Explicando o contrato CrossChainPriceNFT.sol

1. Licen√ßa e Pragma
- // SPDX-License-Identifier: MIT: Define a licen√ßa MIT usada pelo contrato.
- pragma solidity 0.8.19;: Especifica a vers√£o Solidity utilizada para compilar o contrato (vers√£o 0.8.19).

2. Importa√ß√µes
- import {CCIPReceiver} from "@chainlink/contracts-ccip/src/v0.8/ccip/applications/CCIPReceiver.sol";: Importa a funcionalidade CCIPReceiver da biblioteca Chainlink CCIP para receber mensagens entre blockchains.
- import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";: Importa a biblioteca Client da Chainlink CCIP para lidar com mensagens de forma mais gen√©rica (n√£o utilizada diretamente neste contrato).

3. Interface InftMinter
- Define uma interface InftMinter com a fun√ß√£o mintFrom para abstrair a intera√ß√£o com o contrato de cunhagem de NFTs. Essa interface permite que qualquer contrato que implemente a fun√ß√£o mintFrom seja utilizado pelo CrossDestinationMinter.

4. Contrato CrossDestinationMinter
- contract CrossDestinationMinter is CCIPReceiver**: Define o contrato CrossDestinationMinter que herda a funcionalidade CCIPReceiver para receber mensagens entre blockchains.

5. Vari√°vel nft
- InftMinter public nft;: Declara uma vari√°vel p√∫blica do tipo InftMinter para armazenar o endere√ßo do contrato de cunhagem de NFT.

6. Evento MintCallSuccessfull
- event MintCallSuccessfull();: Define um evento para registrar quando uma chamada de cunhagem de NFT √© bem-sucedida.

7. Endere√ßo do Roteador Sepolia
- // https://docs.chain.link/ccip/supported-networks/testnet: Coment√°rio com um link para a documenta√ß√£o sobre redes suportadas pelo CCIP (aqui indicando a rede de teste Sepolia).
- address routerSepolia = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;: Define o endere√ßo do Roteador CCIP para a rede Sepolia (usado para valida√ß√£o de mensagens).

8. Construtor (constructor)
- constructor(address nftAddress) CCIPReceiver(routerSepolia) { ... }: O construtor recebe o endere√ßo do contrato de cunhagem de NFT como argumento e:
    - Chama o construtor da classe base CCIPReceiver passando o endere√ßo do roteador Sepolia.
    - Inicializa a vari√°vel nft com o endere√ßo do contrato de cunhagem de NFT fornecido.

9. Fun√ß√£o _ccipReceive
- function _ccipReceive(Client.Any2EVMMessage memory message) internal override { ... }: Esta fun√ß√£o interna sobreescreve a fun√ß√£o herdada _ccipReceive.
    - Ela √© chamada automaticamente quando o contrato recebe uma mensagem CCIP.
    - A fun√ß√£o verifica se a mensagem foi bem-sucedida e, em seguida, tenta executar os dados da mensagem (message.data) como uma chamada de fun√ß√£o no contrato de cunhagem de NFT (address(nft))
    - Se a chamada for bem-sucedida, o evento MintCallSuccessfull √© emitido.

10. Fun√ß√£o testMint
- function testMint() external { ... }: Esta fun√ß√£o p√∫blica simula uma chamada de cunhagem de NFT na rede Sepolia.
    - Ela chama diretamente a fun√ß√£o mintFrom do contrato de cunhagem de NFT, passando o endere√ßo do remetente da transa√ß√£o (msg.sender) e o ID da rede Sepolia (0).

11. Fun√ß√£o testMessage
- function testMessage() external { ... }: Esta fun√ß√£o p√∫blica demonstra como enviar uma mensagem CCIP para cunhar um NFT na rede Sepolia.
    - Ela codifica uma chamada √† fun√ß√£o mintFrom do contrato de cunhagem de NFT, passando o endere√ßo do remetente e o ID da rede Sepolia.
    - Em seguida, ela tenta executar essa mensagem como uma chamada de fun√ß√£o no contrato de cunhagem de NFT.
    - Se a chamada for bem-sucedida, o evento MintCallSuccessfull √© emitido.

12. Fun√ß√£o updateNFT
- function updateNFT(address nftAddress) external { ... }: Esta fun√ß√£o p√∫blica permite atualizar o endere√ßo do contrato de cunhagem de NFT.
    - Ela atribui o novo endere√ßo √† vari√°vel nft.

-------------------------------------------------------------------------------
Deploy
Parametro - Endere√ßo do NFT

Nome - CrossDestinationMinter address
Sol - 0x33982428Be21eC88C45f48B7BEDE39bD5361E4D7
Pedr0x - 0x3640a0Cd803a2EcEC65f6E52EA3aC0D1eDc0D00e
Jeff Souza - 0xb1C8D12957fdc4219Bb732fd9dF30EA88f97ED5F
Marina - 0x054dD983Ad380B8A706Bf544cB07a4aD1e459e28
Raphael - 0x8147b60CF0424159bc099d53bEeAC3F314A171Cb
Alexandre Eduardo - 0x59e595849D90c854638860ff93fC2099780ce491
Jos√© Aldo - 0xB1A040A19ae367BDDF506Cdc34CcEca32cDdd551

***********************

testMint
testMessage


Origem
Avalanche Fuji

Destino
Ethereum Sepolia

Addicionar Fuji no Metamask
https://chainlist.org/chain/43113

Connect wallet
Avalanche Fuji Testnet - Add to Metamask

Add LINK on Fuji on Metamask
https://docs.chain.link/resources/link-token-contracts#fuji-testnet

https://faucets.chain.link/fuji
LINK e Avax Fuji

Nao conseguiu? Tenta aqui
Somente no workshop
https://workshop-faucet.vercel.app/faucets
BigMac777


consegui 2 AVAX no faucet: https://core.app/tools/testnet-faucet/?subnet=c&token=c n√£o precisou de GitHub

Ah! eu vi que tinha o cupom opcional mas como funcionou sem eu nem li que tinha de ter na mainnet

No Remix
Icone 5 - DEPLOY & RUN TRANSACTIONS
ENVIRONMENT
Verifica a rede conectada
Tem que ser
Custom (43113) network

No Remix
Icon2 2 - FILE EXPLORER
Criar
CrossSourceMinter.sol

******************************* In√≠cio do c√≥digo *******************************
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.19;

// Deploy this contract on Fuji

import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";
import {LinkTokenInterface} from "@chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol";

/**
 * THIS IS AN EXAMPLE CONTRACT THAT USES HARDCODED VALUES FOR CLARITY.
 * THIS IS AN EXAMPLE CONTRACT THAT USES UN-AUDITED CODE.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract CrossSourceMinter {

    // Custom errors to provide more descriptive revert messages.
    error NotEnoughBalance(uint256 currentBalance, uint256 calculatedFees); // Used to make sure contract has enough balance to cover the fees.
    error NothingToWithdraw(); // Used when trying to withdraw but there's nothing to withdraw.

    IRouterClient public router;
    LinkTokenInterface public linkToken;
    uint64 public destinationChainSelector;
    address public owner;
    address public destinationMinter;

    event MessageSent(bytes32 messageId);

    constructor(address destMinterAddress) {
        owner = msg.sender;

        // https://docs.chain.link/ccip/supported-networks/testnet

        // from Fuji
        address routerAddressFuji = 0xF694E193200268f9a4868e4Aa017A0118C9a8177;
        router = IRouterClient(routerAddressFuji);
        linkToken = LinkTokenInterface(0x0b9d5D9136855f6FEc3c0993feE6E9CE8a297846);
        linkToken.approve(routerAddressFuji, type(uint256).max);

        // to Sepolia
        destinationChainSelector = 16015286601757825753;
        destinationMinter = destMinterAddress;
    }

    function mintOnSepolia() external {
        // Mint from Fuji network = chain[1]
        Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
            receiver: abi.encode(destinationMinter),
            data: abi.encodeWithSignature("mintFrom(address,uint256)", msg.sender, 1),
            tokenAmounts: new Client.EVMTokenAmount[](0),
            extraArgs: Client._argsToBytes(
                Client.EVMExtraArgsV1({gasLimit: 980_000})
            ),
            feeToken: address(linkToken)
        });        

        // Get the fee required to send the message
        uint256 fees = router.getFee(destinationChainSelector, message);

        if (fees > linkToken.balanceOf(address(this)))
            revert NotEnoughBalance(linkToken.balanceOf(address(this)), fees);

        bytes32 messageId;
        // Send the message through the router and store the returned message ID
        messageId = router.ccipSend(destinationChainSelector, message);
        emit MessageSent(messageId);
    }

    modifier onlyOwner() {
        require(msg.sender == owner);
        _;
    }

    function linkBalance (address account) public view returns (uint256) {
        return linkToken.balanceOf(account);
    }

    function withdrawLINK(
        address beneficiary
    ) public onlyOwner {
        uint256 amount = linkToken.balanceOf(address(this));
        if (amount == 0) revert NothingToWithdraw();
        linkToken.transfer(beneficiary, amount);
    }
}
```
***************************** Fim do c√≥digo *****************************

#### Explicando o contrato CrossChainPriceNFT.sol

1. Licen√ßa e Pragma
- // SPDX-License-Identifier: MIT: Define a licen√ßa MIT usada pelo contrato.
- pragma solidity 0.8.19;: Especifica a vers√£o Solidity utilizada para compilar o contrato (vers√£o 0.8.19).

2. Importa√ß√µes
- import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";: Importa a interface IRouterClient da biblioteca Chainlink CCIP para interagir com o roteador CCIP.
- import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";: Importa a biblioteca Client da Chainlink CCIP para auxiliar na constru√ß√£o e envio de mensagens CCIP.
- import {LinkTokenInterface} from "@chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol";: Importa a interface LinkTokenInterface para interagir com o contrato do token LINK da Chainlink.

3. Contrato CrossSourceMinter
- contract CrossSourceMinter { ... }: Define o contrato CrossSourceMinter respons√°vel por enviar mensagens de cunhagem de NFT para outra blockchain.

4. Defini√ß√£o de Erros Personalizados
- Define dois erros personalizados para fornecer mensagens de revers√£o mais informativas:
    - NotEnoughBalance: Indica que o contrato n√£o tem saldo suficiente para cobrir as taxas de envio da mensagem.
    - NothingToWithdraw: Indica que n√£o h√° saldo de LINK para ser retirado. 

5. Vari√°veis de Estado
- IRouterClient public router;: Armazena o endere√ßo do roteador CCIP com o qual o contrato se comunica.
LinkTokenInterface public linkToken;: Armazena o endere√ßo do contrato do token LINK da Chainlink.
uint64 public destinationChainSelector;: Armazena o identificador da blockchain destino (Sepolia neste caso).
- address public owner;: Armazena o endere√ßo do propriet√°rio do contrato.
- address public destinationMinter;: Armazena o endere√ßo do contrato de cunhagem de NFT na blockchain destino.

6. Evento MessageSent
- Define um evento para registrar o envio de uma mensagem CCIP e seu identificador.

7. Construtor (constructor)
- constructor(address destMinterAddress) { ... }: O construtor recebe o endere√ßo do contrato de cunhagem de NFT na blockchain destino como argumento e:
    - Define o endere√ßo do propriet√°rio do contrato como o remetente da transa√ß√£o (msg.sender).
    - Carrega os endere√ßos do roteador CCIP e do contrato LINK na rede Fuji a partir de valores codificados (n√£o use em produ√ß√£o).
    - Aprova o gasto ilimitado de LINK para o roteador CCIP (somente para fins de demonstra√ß√£o).
    - Define o identificador da blockchain destino (Sepolia).
    - Define o endere√ßo do contrato de cunhagem de NFT na blockchain destino. 

8. Fun√ß√£o mintOnSepolia
- function mintOnSepolia() external { ... }: Esta fun√ß√£o p√∫blica permite solicitar a cunhagem de um NFT na blockchain destino (Sepolia).
    - Constr√≥i uma mensagem CCIP do tipo EVM2AnyMessage para solicitar a cunhagem:
    - Define o endere√ßo do contrato de cunhagem de NFT na blockchain destino como receptor da mensagem.
    - Codifica a chamada √† fun√ß√£o mintFrom do contrato de cunhagem de NFT, passando o endere√ßo do remetente da transa√ß√£o (msg.sender) e o ID da rede Fuji (1).
    - Define que n√£o h√° tokens para enviar junto com a mensagem.
    - Define os argumentos extras da mensagem, incluindo o limite de g√°s desejado (980_000).
    - Define o token LINK como token de pagamento das taxas.
- Obt√©m a taxa necess√°ria para enviar a mensagem usando router.getFee.
- Verifica se o contrato possui saldo suficiente de LINK para pagar as taxas.
- Envia a mensagem CCIP usando router.ccipSend e armazena o identificador da mensagem.
- Emite o evento MessageSent para indicar que a mensagem foi enviada com sucesso.

9. Modificador onlyOwner
- modifier onlyOwner() { ... }: Este modificador restringe a execu√ß√£o de uma fun√ß√£o ao propriet√°rio do contrato. 

10. Fun√ß√£o linkBalance
- function linkBalance (address account) public view returns (uint256) { ... }: Esta fun√ß√£o p√∫blica retorna o saldo de LINK de um determinado endere√ßo.

11. Fun√ß√£o withdrawLINK
- function withdrawLINK(address beneficiary) public onlyOwner { ... }: Esta fun√ß√£o p√∫blica permite que o propriet√°rio do contrato retire o saldo de LINK do contrato para um endere√ßo especificado.
    - Verifica se h√° saldo de LINK para retirar.
    - Transfere o saldo de LINK para o endere√ßo do benefici√°rio.

---------------------------------------------------------------------------------------
Deploy
Parametro: CrossDestinationMinter address
(contrato que voce publicou antes, na Sepolia)

Nome - CrossSourceMinter address
Sol - 0x08dFEBD8f2868cb724A578f8368F62678489d4Af
Pedr0x - 0x6e5FB12622F1B897ae14e702A88174C6aBd51676
Marina - 0x58D21d4ce5cc1F0A53c8751e46AAcDA841944509
Raphael - 0x72f541e792575EA506E3f698112CDA4d41df26Fa
Jeff Souza - 0x74B6C42ed5859A8d73A0D641090273841A276D06
Jos√© Aldo - 0xB1A040A19ae367BDDF506Cdc34CcEca32cDdd551
Alexandre Eduardo - 0x47b423a3a0EAD547f2f3312fCFE6307666cC64AA


Add LINK na Fuji - Metamask
0x0b9d5D9136855f6FEc3c0993feE6E9CE8a297846

No Metamask
Enviar 5 LINK para o endenreco do CrossSourceMinter 

No Remix
Executar
MintOnSepolia

No Metamask - copy transaction id
Nome - TX Id
Sol - 0x3f1f977382549bbf795941bb67e479a4090af18138196759f96ad64b17288be2
Raphael - 0xa83ddbfa9517e5012ebb5eac8e3ebc1ee1b5784e9b45850de3d36d996b72a712
Marina -
0xc17099b7ec1562070c35b0eeedbede3966313c87beb2cb7fe4963cebe7862ce5
0x013397aeedff03a240bf827914828855afb53c4d2159489d99d396d2d6dd6136
Jeff Souza - 0x279c7f7d072dac29e6759964c8c8965348111aee63bbeb541ce2009c392dbb03
Sol2 - 0x0d5fe63d8e495550415546104db3fd4d826d6fb4032c47bb903e6e6afe847e33
Alexandre Eduardo - 0x56d0a8b53c239783d9f55cb933f9c0620beb21c2e5bc15b7f45e945e7adc2679
Pedr0x - 0x6e2a9099c5f131586f2b400523e4cbe22db4180a623f1542095c5c238471ca2d
Jos√© Aldo - 0x33c2ce8abc6ea6da9a3986ced48bd3e8cceda277ceaa06e277591bab613ce712
*******************************************

https://ccip.chain.link/
Pesquisar o hash da transacao